let go = //{./go};
let sysl = //{./sysl};
let grpc = //{./grpc};

\(
    :app,
    :appname,
    :basepath,
    :clientDeps,
    :endpoints,
    :goModule,
    :hasDB,
    :module,
    :nonRestEndpoints,
    ...
)
    let client = //{./client}((:appname, :clientDeps, :hasDB, :module));
    let goAppname = go.name(grpc.app.name(app));
    let authorizationRule = \ep cond ep {
        {'attrs': {'authorization_rule': {'s': (s: rule)}, ...}, ...} : rule,
    };
    $`
        ${go.prelude(app, (clientDeps => $`${basepath}/${.import}`) | {go.pbImport(app)})}

        // GrpcServiceHandler for ${appname} API
        type GrpcServiceHandler struct {
            pb.Unimplemented${goAppname}Server
            genCallback         core.GrpcGenCallback
            serviceInterface    *GrpcServiceInterface
            authorizationRules  map[string]authrules.Rule
            ${clientDeps orderby . >> (goModule.depField(.) -> $`${.name} ${.type}`)::\i}
        }

        // NewGrpcServiceHandler for ${appname}
        func NewGrpcServiceHandler(
            ctx context.Context,
            hooks *core.Hooks,
            genCallback core.GrpcGenCallback,
            serviceInterface *GrpcServiceInterface,
            ${clientDeps orderby . >> (goModule.depField(.) -> $`${.name} ${.type},`)::\i}
        ) (*GrpcServiceHandler, error) {
            ${cond {hasDB: $`
                db, dberr := database.GetDBHandle()
                if dberr != nil {
                        return nil, dberr
                }
            `}}

            authorizationRules := make(map[string]authrules.Rule)
            ${nonRestEndpoints >> \(@value: ep, ...)
                let method = go.name(ep('name').s);
                let ruleExpression = authorizationRule(ep);
                cond {authorizationRule(ep): $`
                    authRule${method}, authRuleErr := core.ResolveGRPCAuthorizationRule(ctx, hooks, "${method}", ${"`"++ruleExpression++"`"})
                    if authRuleErr != nil {
                        return nil, authRuleErr
                    }
                    authorizationRules["${method}"] = authRule${method}
                `}
            ::}

            return &GrpcServiceHandler{
                genCallback: genCallback,
                serviceInterface: serviceInterface,
                authorizationRules: authorizationRules,
                ${clientDeps orderby . >> (goModule.depField(.) -> $`${.name}: ${.name},`)::\i}
            }, nil
        }

        // RegisterServer registers the ${appname} gRPC service
        func (s *GrpcServiceHandler) RegisterServer(ctx context.Context, server *grpc.Server) {
            pb.Register${goAppname}Server(server, s)
        }

        ${nonRestEndpoints >> \(@value: ep, ...)
            let method = go.name(ep('name').s);
            let requestType = go.leafType(ep('param').a(0)('type'));
            let returns = sysl.endpoint.normalReturns(ep);
            let returnTypes = returns >> go.name(.type(.type count - 1));
            let returnType = returnTypes(returnTypes count - 1);

            $`
                // ${method} ...
                func (s *GrpcServiceHandler) ${method}(ctx context.Context, req *pb.${requestType}) (*pb.${returnType}, error) {
                    if s.serviceInterface.${method} == nil {
                        return nil, status.Errorf(codes.Unimplemented, "method ${method} not implemented")
                    }

                    ctx, cancel := s.genCallback.DownstreamTimeoutContext(ctx)
                    defer cancel()
                    ${cond {authorizationRule(ep): $`
                        if _, ok := s.authorizationRules["${method}"]; !ok {
                            return nil, status.Errorf(codes.Unimplemented, "authorization rule for method ${method} not implemented")
                        }
                        ctx, err := s.authorizationRules["${method}"](ctx)
                        if err != nil {
                            return nil, err
                        }
                    `}}
                    ${cond {client.notEmpty(ep): $`client := ${client.new(ep)}`}}
                    return s.serviceInterface.${method}(ctx, req${cond {client.notEmpty(ep): `, client`}})
                }
            `
        ::}
    `
