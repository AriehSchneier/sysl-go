let go = //{./go};
let sysl = //{./sysl};

\(:app, :appname, :endpoints, :clientDeps, :module, :basepath, ...)
    let (:methodSignature, :typeWithClientPackage, :methodReturnTypes, ...) = go.module(module);

    let HttpMethodStatements = \ep
        let patterns = sysl.patterns(ep);
        let returns = sysl.endpoint.returns(app, ep);

        let nils = methodReturnTypes(app, ep) where .@item != "error" orderby .@ >>
            cond { .@item = "string": '""', _ : "nil" };

        let declareResp = \var
            let r = returns where .@item.var = var && typeWithClientPackage(app, .@item) >>
                $`var ${var}Response ${//seq.trim_prefix('*', typeWithClientPackage(app, .))}`
                rank(@:.@item);
            # just the first type for now, todo: remove where .@ < 1
            r where .@ < 1;

        let returnDownstreamError = \type \var \err
            $`return ${nils ++ [
                $`common.CreateDownstreamError(ctx, common.Downstream${type}Error, ${var}.HTTPResponse, ${var}.Body, ${err})`
            ]::, }`;

        $`
            required := []string{${
                (ep("param")?.a:{} where {"header"} (<=) sysl.patterns(.@item('type')) && sysl.type.required(.@item('type'))) rank (:.@) >>
                    $`"${.('type')?('attrs')?('name')?('s').s:(.('name').s)}"`
            ::, }}
            ${declareResp("ok")::\i}
            ${declareResp("error")::\i}
            ${#`
                let pathvars = ep('restParams')('urlParam')?.a:{};
                let terms = pathvars => $`{${.@item('name').s}}`;
                let formatArgs = $`"%s${//seq.split("/")(ep('restParams')('path').s) >> cond {. <: terms: "%v", _: .}::/}"`; #`
                let params = pathvars >> $`req.${go.name(.('name').s)}`;
                $`u, err := url.Parse(fmt.Sprintf(${formatArgs}, ${["s.URL"] ++ params::, }))`
            }
            if err != nil {
                return ${nils ++ [`common.CreateError(ctx, common.InternalError, "failed to parse url", err)`]::, }
            }

            ${let queryParams = ep('restParams')('queryParam')?.a:{}; cond {queryParams: $`
                q := u.Query()
                ${
                    let reqQueryParams = queryParams where !.@item('type')('opt')?.b:false;
                    let optQueryParams = queryParams where .@item('type')('opt')?.b:false;
                    let queryParamName = \qparam cond {
                        'queryParamNames' <: qparam('attrs')?:{}:
                            $"${//seq.concat(qparam('attrs')?('queryParamNames'):{} where (qparam.name <: .) >> (. &~ {qparam.name}))::}",
                        _: qparam('name').s,
                    };
                    let var = \. $`req.${go.name(.('name').s)}`;
                    $`
                        ${reqQueryParams => .@item orderby sysl.source(.('type')) >> $`
                            q = convert.EncodeQueryParam(q, "${queryParamName(.)}", ${var(.)})
                        `::\i}
                        ${optQueryParams => .@item orderby sysl.source(.('type')) >> $`
                            if ${var(.)} != nil {
                                q = convert.EncodeQueryParam(q, "${queryParamName(.)}", *${var(.)})
                            }
                        `::\i}
                    `
                }
                u.RawQuery = q.Encode()
            `}}
            ${
                let body = (ep('param')?.a:{} where "body" <: sysl.patterns(.@item('type'))) >> "req.Request";
                let retvars = returns where .@item.type != [""] => .@item.var;
                let method = ep('restParams')('method').s;
                $`
                    result, err := restlib.DoHTTPRequest2(ctx, &restlib.HTTPRequest{
                        Client:        s.Client,
                        Method:        "${method}",
                        URLString:     u.String(),
                        Body:          ${body || ["nil"]::},
                        Required:      required,
                        OKResponse:    ${cond {"ok" <: retvars: "&okResponse", _: "nil"}},
                        ErrorResponse: ${cond {"error" <: retvars: "&errorResponse", _: "nil"}},
                        ExtraHeaders:  s.Headers,
                    })
                `
            }
            restlib.OnRestResultHTTPResult(ctx, result, err)
            if err != nil {
                ${
                    let downstreamUnavailable = $`
                        return ${nils ++ [$`
                            common.CreateError(ctx, common.DownstreamUnavailableError, "call failed: ${appname} <- ${ep('restParams')('method').s} " + u.String(), err)`
                        ]::, }`;
                    cond {
                        "error" <: (returns where .@item.type != [""] => .@item.var): $`
                            response, ok := err.(*restlib.HTTPResult)
                            if !ok {
                                ${downstreamUnavailable}
                            }
                            ${returnDownstreamError("Response", "response", "&errorResponse")}
                        `,
                        _: downstreamUnavailable
                    }
                }
            }

            if result.HTTPResponse.StatusCode == http.StatusUnauthorized {
                ${returnDownstreamError("Unauthorized", "result", "nil")}
            }
            ${
                let makeType = \input
                    cond {
                        input = "error": input,
                        _: go.name(input),
                    };

                (returns where (.@item -> .var != "error" && .type != [""]) rank(@: typeWithClientPackage(app, .@item))) >>> \i \.
                    let responseVar = $`${makeType(.var)}${go.name(//seq.concat(.type))}Response`;
                    let responseType = typeWithClientPackage(app, .);

                    # The current implementation of restlib#unmarshal returns pointer references for raw string
                    # and byte responses in order to handle aliased string and byte responses also. For this reason,
                    # raw string and byte responses must be dereferenced before returned.
                    let ptrResponseType = cond { responseType <: {'string', '[]byte'}: '*' ++ responseType, _: responseType };
                    let ptrResponseVar = cond { responseType <: {'string', '[]byte'}: '*' ++ responseVar, _: responseVar };

                    # The block below also avoids validating raw string and byte responses. The
                    # Validator#validate method is designed to validate structs, and while this method does
                    # include logic to regard all strings as valid it is not primarily intended for such
                    # usage, therefore non-struct responses (i.e. string and byte) responses are not validated.
                    $`
                        ${responseVar}, ok := result.Response.(${ptrResponseType})
                        if ok {
                            ${cond { responseType !<: {'string', '[]byte'}: $`
                                valErr := validator.Validate(${responseVar})
                                if valErr != nil {
                                    ${returnDownstreamError(`UnexpectedResponse`, `result`, `valErr`)}
                                }
                            `}}

                            ${
                                cond {
                                    returns where .@item.type = [""] && .@item.var = "ok": $`return ${ptrResponseVar}, &result.HTTPResponse.Header, nil`,
                                    # return response in correct location for function signature (nil otherwise)
                                    _: $`return ${sysl.replaceArrayElement(nils, i, ptrResponseVar) ::, }, nil`,
                                }
                            }
                        }
                    `
            ::\i}
            ${cond {
                returns where .@item.type != [""] && .@item.var = "ok": returnDownstreamError("UnexpectedResponse", "result", "nil"),
                returns where .@item.type = [""] && .@item.var = "ok": "return &result.HTTPResponse.Header, nil",
                _: "return nil",
            }}
        `;

    $`
        ${go.prelude(app, clientDeps => $`${basepath}/${.import}`)}
        ${"app.union" where false >> let f = $`Is${.key}`; $`
            // ${f} ...
            type ${go.name(.key)} interface {
                ${f}()
            }

            ${.value.fields >> $`func (*${.}) ${f} {}`}
        `:::\n}

        // Service interface for ${appname}
        type Service interface {
            ${endpoints >> methodSignature(app, .@value)::\i}
        }

        // Client for ${appname} API
        type Client struct {
            Client  *http.Client
            URL     string
            Headers map[string][]string
        }

        // NewClient for ${appname}
        func NewClient(client *http.Client, serviceURL string) *Client {
            return &Client{client, serviceURL, nil}
        }

        ${endpoints where cond .@item.@value {{'restParams': _, ...}: true} >> \(@value: ep, ...) $`
            // ${go.methodName(app, ep)} ...
            func (s *Client) ${methodSignature(app, ep)} {
                ${HttpMethodStatements(ep)}
            }
        `::\i}
    `
