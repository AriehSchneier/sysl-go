let go = //{./go};
let grpc = //{./grpc};
let sysl = //{./sysl};

\(:app, :appname, :basepath, :clientDeps, :endpoints, :fixPBPath, :module, ...)
    let methodInfos = endpoints where ('workflow' <: sysl.patterns(.@item.@value)) =>
        \(@:_, @item: (@:_, @value: ep))
            go.temporalMethodInfo(module, app, app, ep)
    ;
    let methodInfos = methodInfos orderby .name;
    let appname = go.name(grpc.app.name(app));

    $`
        ${go.prelude(app, (clientDeps => $`${basepath}/${.import}`))}

        const (
            // Task Queue name for ${appname}.
            ${appname}TaskQueue = ${appname:q}

            // Workflow names
            ${methodInfos >> $`${.name}Name = ${.name:q}`::\n}
        )

        // Service interface for ${appname}.
        type Service interface {
            ${methodInfos >> $`${.name}${.sig(false)}`::\i}
        }

        // Client implements a client for ${appname}.
        type Client struct {
            Client client.Client
        }

        // NewClient creates a new Client.
        func NewClient(c client.Client) *Client {
            return &Client{c}
        }

        ${methodInfos >> $`
            // ${.name} ...
            func (s *Client) ${.name}${.sig(false)} {
                return core.ExecuteWorkflow[${.responseType.leaf || `any`}](
                    ctx,
                    s.Client,
                    ${appname}TaskQueue,
                    ${.name}Name,
                    ${.requestType >> .name::,\n:,}
                )
            }
        `::\n\n:}
    `
