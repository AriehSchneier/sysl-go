let go = //{./go};
let sysl = //{./sysl};

\(:app, :appname, :clientDeps, :endpoints, :goModule, :hasDB, :basepath, :module, :nonRestEndpoints, ...)
    let client = //{./client}((:appname, :clientDeps, :hasDB, :module));
    let structName = $`Default${go.name(appname)}Impl`;
    $`
        ${go.prelude(app, (clientDeps => $`${basepath}/${.import}`) | go.pbPackage(app))}

        ${nonRestEndpoints >> \(@:_, @value: ep) client.clientStruct(ep)::\i}

        // GrpcServiceInterface for ${appname}
        type GrpcServiceInterface struct {
            ${nonRestEndpoints >> \(@value: ep, ...)
                let name = go.methodName(ep);
                $`${name} func(ctx context.Context, req *pb.${name}Request${cond {client.notEmpty(ep): $`, client ${name}Client`}}) (*pb.${name}Response, error)`
            ::\i}
        }

        ${client.config}
    `
